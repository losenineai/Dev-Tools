plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':dx-axml')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task fatJar(type: Jar, dependsOn: jar) {
    baseName = project.name
    def deps = configurations.runtime

    def depClasses = { deps.collect { it.isDirectory() ? it : zipTree(it) } }

    from(depClasses) {
        exclude 'META-INF/*.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.DSA'
        exclude '**/*.html'
    }

    manifest {
        attributes 'Implementation-Title': 'ApplicationInfoCollect'
        attributes 'Main-Class': 'com.dingxiang.Main'
    }

    from(sourceSets.main.output)

    from 'resources'
}

publishing {
    publications {
        maven(MavenPublication) {
            //指定group/artifact/version信息，可以不填。默认使用项目group/name/version作为groupId/artifactId/version
            groupId(GROUP_ID)
            artifactId(MUDULE_NAME)
            version(VERSION)
            //如果是war包填写components.web，如果是jar包填写components.java
            from(components.java)

            artifact fatJar {
                classifier = 'deploy'
            }

            pom.withXml {
                def node = asNode()
                def rmNode = node.get('dependencies')
                node.remove(rmNode)
            }
        }
    }
    repositories {
        maven {
            //指定要上传的maven私服仓库
            url = MAVEN_REPO_RELEASE_URL
            //认证用户和密码
            credentials {
                username NEXUS_USERNAME
                password NEXUS_PASSWORD
            }
        }
    }
}